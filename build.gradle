plugins {
	id 'fabric-loom' version '0.11-SNAPSHOT'
	id 'io.github.juuxel.loom-quiltflower' version '1.6.0' // For loom 0.8-0.9, 0.11+
	id 'org.quiltmc.quilt-mappings-on-loom' version '4.0.0'
	id 'java-library'
	id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = "${project.mod_version}+${project.minecraft_version}"
group = project.maven_group

repositories {
	// Add repositories to retrieve artifacts from in here.
	// You should only use this when depending on other mods because
	// Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
	// See https://docs.gradle.org/current/userguide/declaring_repositories.html
	// for more information about repositories.
	maven {
		name 'QuiltSnapshot'
		url 'https://maven.quiltmc.org/repository/snapshot'
		content {
			includeGroup 'org.quiltmc.fabric_api_qsl'
			includeGroupByRegex 'org.quiltmc.qsl(.[a-z_]+)?'
		}
	}
	maven {
		name = 'Ladysnake Mods'
		url = 'https://ladysnake.jfrog.io/artifactory/mods'
	}
}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings(loom.layered {
		addLayer(quiltMappings.mappings("org.quiltmc:quilt-mappings:${minecraft_version}+build.${project.quilt_mappings}:v2"))
	})
//	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	modImplementation "org.quiltmc.qsl:qsl:${project.qsl_version}+${project.minecraft_version}-SNAPSHOT"
	//fapi-qsl networking so cca can work
	modLocalRuntime include("org.quiltmc.fabric_api_qsl:fabric-api-base:1.0.0+${project.minecraft_version}-SNAPSHOT")
	modLocalRuntime include("org.quiltmc.fabric_api_qsl:fabric-networking-api-v1:2.0.0+${project.minecraft_version}-SNAPSHOT")
	modLocalRuntime include("org.quiltmc.fabric_api_qsl:fabric-registry-sync-v0:0.9.6+${project.minecraft_version}-SNAPSHOT")

	include "org.quiltmc.qsl:block:1.0.0:${project.minecraft_version}-SNAPSHOT"
	include "org.quiltmc.qsl:core:1.0.0:${project.minecraft_version}-SNAPSHOT"
	include "org.quiltmc.qsl:gui:1.0.0:${project.minecraft_version}-SNAPSHOT"
	include "org.quiltmc.qsl:item:1.0.0:${project.minecraft_version}-SNAPSHOT"

	//having both fapi networking and qsl networking causes a redirect war
	modImplementation("dev.onyxstudios.cardinal-components-api:cardinal-components-base:${project.cardinal_version}") {
		exclude group: "net.fabricmc.fabric-api"
	}
	modImplementation("dev.onyxstudios.cardinal-components-api:cardinal-components-level:${project.cardinal_version}") {
		exclude group: "net.fabricmc.fabric-api"
	}
	include("dev.onyxstudios.cardinal-components-api:cardinal-components-base:${project.cardinal_version}") {
		exclude group: "net.fabricmc.fabric-api"
	}
	include("dev.onyxstudios.cardinal-components-api:cardinal-components-level:${project.cardinal_version}") {
		exclude group: "net.fabricmc.fabric-api"
	}
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	// Minecraft 1.18 (1.18-pre2) upwards uses Java 17.
	it.options.release = 17
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}